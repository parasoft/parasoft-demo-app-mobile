plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'io.appium:java-client:8.2.0'
    testImplementation 'org.seleniumhq.selenium:selenium-support:4.4.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation 'io.rest-assured:rest-assured:5.2.0'
    testImplementation 'com.google.code.gson:gson:2.9.1'
}

apply plugin:SystemPropertiesMappingPlugin

test {
    useJUnitPlatform()

    mappedSystemProperties= [
            'appium.profile.name',
            'device.name',
            'appium.server.url',
            'adb.server.port',
            'demo.app.apk.path',
            'pda.server.base-url',
            'duration.of.seconds',
            'uiautomator2.server.launch.timeout'
    ]
}

class SystemPropertiesMappingPlugin implements Plugin<Project>{
    void apply(Project project){
        project.tasks.withType(Test){ testTask ->
            testTask.ext.mappedSystemProperties = []
            doFirst{
                mappedSystemProperties.each{ mappedPropertyKey ->
                    def systemPropertyValue = System.getProperty(mappedPropertyKey)
                    if(systemPropertyValue){
                        testTask.systemProperty(mappedPropertyKey, systemPropertyValue)
                    }
                }
            }
        }
    }
}