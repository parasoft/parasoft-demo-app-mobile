plugins {
    id 'com.android.application'
}

apply(plugin: 'jacoco')

def applicationName = "parasoft-demo-app-mobile"

def buildMetadata = ''
if (project.hasProperty("jenkinsBuildNumber")) {
    buildMetadata += new Date().format('yyyyMMdd') + 'B' + jenkinsBuildNumber
} else {
    buildMetadata += new Date().format('yyyyMMddHHmmss')
}
if (project.hasProperty("gitCommitHash")) {
    buildMetadata += '.' + gitCommitHash
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.parasoft.demoapp"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "buildMetadata", "\"${buildMetadata}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true

            // To modify the name of build apk file
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${applicationName}-" + android.defaultConfig.versionName + ".apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            res.srcDirs = [
                    // Add layout files path
                    'src/main/res/layouts/homeActivity',
                    'src/main/res/layouts/loginActivity',
                    'src/main/res/layouts/common',
                    'src/main/res/'
            ]
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    debugImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'org.mockito:mockito-android:4.8.0'
}

task mergeUnitTestAndAndroidTestReports(type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {
    group = "report"
    description = "Run Android tests and Unit tests and merge the coverage of these two kinds of tests into one report."

    reports {
        html.enabled(true)
        xml.enabled(true)
    }

    doLast {
        println("Html report is generated: ${buildDir}/reports/jacoco/mergeUnitTestAndAndroidTestReports/html/index.html")
        println("Xml report is generated: ${buildDir}/reports/jacoco/mergeUnitTestAndAndroidTestReports/mergeUnitTestAndAndroidTestReports.xml")
    }

    def fileFilter =
            [
                    '**/BuildConfig.*'
            ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.projectDir, includes: ['**/**/*.exec', '**/**/*.ec'])
}